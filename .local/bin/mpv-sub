#!/bin/zsh

# Try to load all available subtitles for mpv not regarding their
# nesting level.
# Based on <https://www.linux.org.ru/news/multimedia/9704338?cid=9715830>.
set -e
setopt EXTENDED_GLOB
setopt NULL_GLOB

# Collect directories of the opened file(s).
local -a mediadirs
for arg in $@; do
    if [[ ${arg[0]} == '-' ]]; then
        continue
    fi
    if [[ -f $arg ]]; then
        # Get directory of the file.
        mediadirs+=( ${arg:A:h} )
    fi
done

# Autoconvert list to string delemited by colons.
local -aT SUBPATHS subpaths
# Collect subtitles directories.
subpaths=( ${^mediadirs}/(#i)*(sub|суб)*{,/**}(/) )

local -a args
if (( ${#subpaths} )); then
    args+=( -sub-paths $SUBPATHS )
fi
args+=( $@ )
echo "Playing with arguments: '$args'" >&2
mpv $args
